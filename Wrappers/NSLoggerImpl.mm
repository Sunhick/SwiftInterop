//  Copyright Â© 2018 Sunil. All rights reserved.

#import <Foundation/Foundation.h>

// This file is auto generated by the swift compiler. It's purpose is to expose
// swift class into objective c++ where Briding-Headers are for exposing -
// objective c++ to swift world.
#import <SwiftInterop-Swift.h>

#import "NSLoggerImpl.hpp"

// The below c++ mixed code is only for c++ calling into swift
NSLoggerImpl::NSLoggerImpl(std::string name) : self(NULL) {
    init(name);
}

NSLoggerImpl::~NSLoggerImpl(void) {
    // ARC forbids explicit message send of 'dealloc'
    // We need to disable ARC for this file/ project using  fno-objc-arc
    
    // Tell ARC to take control of this object and release it for us when it's ref.
    // count reaches zero.
    Logger* logger __unused = (__bridge_transfer Logger*)self;
}

void NSLoggerImpl::init(std::string name) {
    NSString* logName = [NSString stringWithUTF8String:name.c_str()];
    
    // Manually increment the reference count of logger by 1. so that it will be
    // long lived and we will dealloc ourselves later. (that's a lie for now).
    self = (__bridge_retained void*)[[Logger alloc] init:logName];
}

void NSLoggerImpl::info(const char* cmsg) {
    Logger* log = (__bridge Logger*)self;
    if (log != nil) {
        NSString* msg = [[NSString alloc] initWithUTF8String:cmsg];
        [log infoWithMsg:msg];
    }
}

void NSLoggerImpl::debug(const char* cmsg) {
    Logger* log = (__bridge Logger*)self;
    if (log != nil) {
        NSString* msg = [[NSString alloc] initWithUTF8String:cmsg];
        [log debugWithMsg:msg];
    }
}
